apply plugin: 'com.android.library'
apply plugin: 'org.anarres.jarjar'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        consumerProguardFile file('proguard-rules.pro')

    }

    packagingOptions {
        merge 'META-INF/spring.schemas'
        merge 'META-INF/spring.tooling'
        merge 'META-INF/spring.handlers'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
    }

}

configurations.all {
    exclude group: "commons-logging", module: "commons-logging"
}

def springVersion = "4.3.6.RELEASE"

dependencies {

    provided "org.projectlombok:lombok:1.16.16"

    compile 'org.slf4j:slf4j-api:1.7.21'

    compile 'com.android.support:appcompat-v7:25.3.1'

    provided "org.springframework:spring-context:$springVersion"

    compile jarjar.repackage {
        from "org.springframework:spring-aop:$springVersion@jar"
        destinationName = "spring-aop-${springVersion}-android.jar"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
        classRename "org.apache.commons.logging.**", "io.freefair.logging.@1"
    }
    compile jarjar.repackage {
        from "org.springframework:spring-beans:$springVersion@jar"
        destinationName = "spring-beans-${springVersion}-android.jar"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
        classRename "org.apache.commons.logging.**", "io.freefair.logging.@1"
    }
    compile jarjar.repackage {
        from "org.springframework:spring-expression:$springVersion@jar"
        destinationName = "spring-expression-${springVersion}-android.jar"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
        classRename "org.apache.commons.logging.**", "io.freefair.logging.@1"
    }
    compile jarjar.repackage {
        from "org.springframework:spring-core:$springVersion@jar"
        destinationName = "spring-core-${springVersion}-android.jar"

        classDelete "org.springframework.cglib.**"
        classDelete "org.springframework.core.type.classreading.SimpleMetadataReader"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
        classRename "org.apache.commons.logging.**", "io.freefair.logging.@1"
    }
    compile jarjar.repackage {
        from "org.springframework:spring-context:$springVersion@jar"
        destinationName = "spring-context-${springVersion}-android.jar"

        classDelete "org.springframework.ejb.**"
        classDelete "org.springframework.context.annotation.ConfigurationClassEnhancer"
        classDelete "org.springframework.instrument.classloading.**"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
        classRename "org.apache.commons.logging.**", "io.freefair.logging.@1"
    }
    compile jarjar.repackage {
        from 'org.slf4j:jcl-over-slf4j:1.7.21@jar'
        destinationName = "jcl-over-slf4j-1.7.21-android.jar"

        classRename "org.apache.commons.logging.**", "io.freefair.logging.@1"
    }
    compile 'io.freefair:openbeans:2.1'

    testCompile 'junit:junit:4.12'
}
