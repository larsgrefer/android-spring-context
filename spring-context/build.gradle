apply plugin: 'com.android.library'
apply plugin: 'org.anarres.jarjar'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'io.freefair.android-maven-jars'
apply plugin: 'io.freefair.android-ci'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        consumerProguardFile file('proguard-rules.pro')
    }

    packagingOptions {
        merge 'META-INF/spring.schemas'
        merge 'META-INF/spring.tooling'
        merge 'META-INF/spring.handlers'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
        exclude 'org/springframework/ejb/**'
        exclude 'org/springframework/cglib/**'
        exclude 'org/springframework/remoting/rmi/**'
    }

    lintOptions {
        warning "InvalidPackage"
    }
}

configurations.all {
    exclude group: "commons-logging", module: "commons-logging"
}

def springVersion = "4.3.10.RELEASE"

dependencies {

    provided "org.projectlombok:lombok:1.16.18"

    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'com.android.support:support-annotations:26.0.2'

    provided "org.springframework:spring-context:$springVersion"

    compile jarjar.repackage {
        from "org.springframework:spring-aop:$springVersion@jar"
        destinationName = "spring-aop-${springVersion}-android.jar"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
    }
    compile jarjar.repackage {
        from "org.springframework:spring-beans:$springVersion@jar"
        destinationName = "spring-beans-${springVersion}-android.jar"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
    }
    compile jarjar.repackage {
        from "org.springframework:spring-expression:$springVersion@jar"
        destinationName = "spring-expression-${springVersion}-android.jar"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
    }
    compile jarjar.repackage {
        from "org.springframework:spring-core:$springVersion@jar"
        destinationName = "spring-core-${springVersion}-android.jar"

        classDelete "org.springframework.cglib.**"
        //classDelete "org.springframework.asm.**"
        classDelete "org.springframework.core.type.classreading.SimpleMetadataReader"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
    }
    compile jarjar.repackage {
        from "org.springframework:spring-context:$springVersion@jar"
        destinationName = "spring-context-${springVersion}-android.jar"

        classDelete "org.springframework.ejb.**"
        classDelete "org.springframework.jmx.**"
        classDelete "org.springframework.remoting.rmi.**"
        classDelete "org.springframework.context.annotation.ConfigurationClassEnhancer"
        classDelete 'org.springframework.context.annotation.ConfigurationClassEnhancer$*'
        classDelete "org.springframework.instrument.classloading.**"

        classRename "java.beans.**", "com.googlecode.openbeans.@1"
    }
    compile 'org.slf4j:jcl-over-slf4j:1.7.25@jar'
    compile 'io.freefair:openbeans:2.3'

    testCompile 'junit:junit:4.12'
}
